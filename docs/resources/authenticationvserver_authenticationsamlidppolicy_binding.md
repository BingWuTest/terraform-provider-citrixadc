---
subcategory: "Authentication"
---

# Resource: authenticationvserver_authenticationsamlidppolicy_binding

The authenticationvserver_authenticationsamlidppolicy_binding resource is used to bind authenticationsamlidppolicy to authenticationvserver resource.


## Example usage

```hcl
resource "citrixadc_authenticationvserver" "tf_authenticationvserver" {
  name           = "tf_authenticationvserver"
  servicetype    = "SSL"
  comment        = "new"
  authentication = "ON"
  state          = "DISABLED"
}
resource "citrixadc_sslcertkey" "tf_sslcertkey" {
  certkey = "tf_sslcertkey"
  cert    = "/var/tmp/certificate1.crt"
  key     = "/var/tmp/key1.pem"
}
resource "citrixadc_authenticationsamlidpprofile" "tf_samlidpprofile" {
  name                        = "tf_samlidpprofile"
  samlspcertname              = citrixadc_sslcertkey.tf_sslcertkey.certkey
  assertionconsumerserviceurl = "http://www.example.com"
  sendpassword                = "OFF"
  samlissuername              = "new_user"
  rejectunsignedrequests      = "ON"
  signaturealg                = "RSA-SHA1"
  digestmethod                = "SHA1"
  nameidformat                = "Unspecified"
}
resource "citrixadc_authenticationsamlidppolicy" "tf_samlidppolicy" {
  name    = "tf_samlidppolicy"
  rule    = "false"
  action  = citrixadc_authenticationsamlidpprofile.tf_samlidpprofile.name
  comment = "aSimpleTesting"
}
resource "citrixadc_authenticationvserver_authenticationsamlidppolicy_binding" "tf_bind" {
  name      = citrixadc_authenticationvserver.tf_authenticationvserver.name
  policy    = citrixadc_authenticationsamlidppolicy.tf_samlidppolicy.name
  bindpoint = "REQUEST"
  priority  = 88
  secondary = "false"
}
```


## Argument Reference

* `name` - (Required) Name of the authentication virtual server to which to bind the policy.
* `policy` - (Required) The name of the policy, if any, bound to the authentication vserver.
* `bindpoint` - (Optional) Bind point to which to bind the policy. Applies only to rewrite and cache policies. If you do not set this parameter, the policy is bound to REQ_DEFAULT or RES_DEFAULT, depending on whether the policy rule is a response-time or a request-time expression.
* `gotopriorityexpression` - (Optional) Expression specifying the priority of the next policy which will get evaluated if the current policy rule evaluates to TRUE.
* `groupextraction` - (Optional) Applicable only while bindind classic authentication policy as advance authentication policy use nFactor
* `nextfactor` - (Optional) On success invoke label.
* `priority` - (Optional) The priority, if any, of the vpn vserver policy.
* `secondary` - (Optional) Applicable only while bindind classic authentication policy as advance authentication policy use nFactor


## Attribute Reference

In addition to the arguments, the following attributes are available:

* `id` - The id of the authenticationvserver_authenticationsamlidppolicy_binding. It is the concatenation of `name` and `policy` attributes seperated by comma.


## Import

A authenticationvserver_authenticationsamlidppolicy_binding can be imported using its id, e.g.

```shell
terraform import citrixadc_authenticationvserver_authenticationsamlidppolicy_binding.tf_bind tf_authenticationvserver,tf_samlidppolicy
```
